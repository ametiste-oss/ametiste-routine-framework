buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply from: "https://raw.githubusercontent.com/ametiste-oss/ametiste-bintray-gradle/master/bintray.gradle"

apply plugin: 'maven-publish'
apply plugin: 'spring-boot'

publishing {
    publications {
        mavenJava(MavenPublication) {

            pom.withXml {
                def parentNode = asNode().appendNode('parent')
                parentNode.appendNode('groupId','org.springframework.boot')
                parentNode.appendNode('artifactId','spring-boot-starter-parent')
                parentNode.appendNode('version',"1.3.1.RELEASE")
            }

            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

bootRepackage {
    enabled = false
}


dependencies {
    compile project(":ametiste-routine-sdk")
    compile("org.springframework.boot:spring-boot")
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
